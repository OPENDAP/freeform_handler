
dnl -*- m4 -*-
dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Id: configure.in,v 1.15 2003/02/10 23:01:53 jimg Exp $)
AC_INIT(FFArray.cc)

AC_CONFIG_HEADER(config_ff.h)
AC_CONFIG_AUX_DIR(../../etc)

AC_PREFIX_DEFAULT("\$(DODS_ROOT)")

DODS_GET_DODS_ROOT
DODS_DIRECTORY_VERSION

DODS_SERVER_VERSION=\"ff-dods/$VERSION\"
AC_DEFINE_UNQUOTED(DODS_SERVER_VERSION, $DODS_SERVER_VERSION)

dnl the command to use to generate dependencies for makefiles

depend=${dods_relative}/etc/depend.sh
AC_SUBST(depend)

dnl flags for the compilers and linkers - set these before locating the
dnl actual tools since some of the AC_PROG macros set these `flag variables'
dnl to default values otherwise.

AC_CANONICAL_HOST

CFLAGS=${CFLAGS-"-gstabs -O2 $CFLAGS"}
CXXFLAGS=${CXXFLAGS-"-gstabs -O2 $CXXFLAGS"}

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_MAKE_SET

DODS_PROG_PERL
DODS_PROG_GTAR
DODS_PROG_LEX
DODS_PROG_BISON

dnl You should have g++, but you can override that with an environment
dnl variable. 

CXX=${CXX-g++}
AC_MSG_RESULT(Setting CXX to $CXX)
AC_SUBST(CXX)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for specific libraries
AC_CHECK_LIB(pthread, pthread_testcancel, 
	[LIBS="$LIBS -lpthread"
	 AC_DEFINE(_REENTRANT)])

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_SETVBUF_REVERSED
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strcspn strerror strspn strstr strtod strtol)

DODS_CHECK_SIZES

DODS_RX_LIB
DODS_WWW_LIB
DODS_COMPRESSION_LIB

dnl DODS_XTRALIBS

DODS_EFENCE

DODS_DBNEW

DODS_DEBUG_OPTION

case "$host" in
    *linux*) CPPFLAGS="$CPPFLAGS -DLINUX" ;;
    *hp*)    CPPFLAGS="$CPPFLAGS -DHP9000" ;;
    *alpha*) CPPFLAGS="$CPPFLAGS -DDEC_ALPHA" ;;
    *sun*)   CPPFLAGS="$CPPFLAGS -DSUN" ;;
    *sgi*)   if test $ac_cv_sizeof_long -eq 4
	     then
	         CPPFLAGS="$CPPFLAGS -DIRIS4"
	     else       
	         CPPFLAGS="$CPPFLAGS -DIRIX"
	     fi ;;
    *)       echo "Either this machine is not supported by FreeForm or"
	     echo "the configure script needs to be fixed?" ;;
esac

dnl There are some defines which must be set with you build code which uses
dnl FreeForm.h. Add them here. 4/17/98 jhrg
CPPFLAGS="$CPPFLAGS -DFREEFORM"

AC_OUTPUT(Makefile FFND/Makefile)

